include('../auto')

/*
 * Objet qui contient l'ensemble des entités vivante alliée et ennemie
 */
class Fight {
	private static _aliveEnemies
	private static _aliveAllies
	private static _aliveAll
	private static _aliveAllLeeks
	private static _aliveAllBulbs
	private static _aliveEnemiesLeeks
	private static _aliveEnemiesBulbs
	private static _aliveAlliesLeeks
	private static _aliveAlliesBulbs
	static allEntitiesId
	static self

	/*
	 * Actualise les tableaux contenant les entités vivante alliée et ennemie ainsi que l'ensemble des entités alliées + ennemies
	 */
	static refresh(){
		_aliveEnemies = []
		_aliveAllies = []
		_aliveAll = []
		_aliveAllLeeks = []
		_aliveAllBulbs = []
		_aliveEnemiesLeeks = []
		_aliveEnemiesBulbs = []
		_aliveAlliesLeeks = []
		_aliveAlliesBulbs = []
		allEntitiesId = []
		for(var e in getAliveEnemies()) {
			var entity = Entity(e)
			_aliveEnemies[e] = entity
			_aliveAll[e] = entity
			if(entity.isBulb){
				_aliveEnemiesBulbs[e] = entity
				_aliveAllBulbs[e] = entity
			} else {
				_aliveEnemiesLeeks[e] = entity
				_aliveAllLeeks[e] = entity
			}
		}
		for(var a in getAliveAllies()) {
			var entity = Entity(a)
			_aliveAllies[a] = entity
			_aliveAll[a] = entity
			if(entity.isBulb){
				_aliveAlliesBulbs[a] = entity
				_aliveAllBulbs[a] = entity
			} else {
				_aliveAlliesLeeks[a] = entity
				_aliveAllLeeks[a] = entity
			}
		}
		self = _aliveAll[getLeek()]
		for(var entity in _aliveAll){
			entity.init()
			push(allEntitiesId, entity.id)
		}
	}

	/*
	 * Renvoie Entity correspondant a l'id de l'entity demandée si elle est vivante
	 * @param entityId Id de l'Entity
	 * @return une Entity 
	 */
	static getEntity(entityId) {
		return _aliveAll[entityId]
	}
	
	/*
	 * Renvoie un tableau contenant les ennemis vivants
	 * @return un tableau d'Entity
	 */
	static getEnemiesAlive() {
		return _aliveEnemies
	}
	
	/*
	 * Renvoie un tableau contenant les ennemis non bulbes vivants
	 * @return un tableau d'Entity
	 */
	static getEnemiesLeeksAlive() {
		return _aliveEnemiesLeeks
	}
	
	/*
	 * Renvoie un tableau contenant les ennemis bulbes vivants
	 * @return un tableau d'Entity
	 */
	static getEnemiesBulbsAlive() {
		return _aliveEnemiesBulbs
	}
	
	/*
	 * Renvoie un tableau contenant les alliés vivants (moi compris dedans)
	 * @return un tableau d'Entity
	 */
	static getAlliesAlive() {
		return _aliveAllies
	}
	
	/*
	 * Renvoie un tableau contenant les alliés non bulbes vivants (moi compris dedans)
	 * @return un tableau d'Entity
	 */
	static getAlliesLeeksAlive() {
		return _aliveAlliesLeeks
	}
	
	/*
	 * Renvoie un tableau contenant les alliés bulbes vivants (moi compris dedans si je suis un bulbe)
	 * @return un tableau d'Entity
	 */
	static getAlliesBulbsAlive() {
		return _aliveAlliesBulbs
	}
	
	/*
	 * Renvoie un tableau contenant les ennemis et les alliés vivants (moi compris dedans)
	 * @return un tableau d'Entity
	 */
	static getAllAlive() {
		return _aliveAll
	}
	
	/*
	 * Détecte les entités vivantes qui jouent avant l'entité entity en paramètre de la fonction
	 * @param entity Entity 
	 * @return liste des entités vivantes alliés et ennemies 
	 */
	 // TODO: cette fonction return une list avec <entity> inclus actuellement, ça ne devrait pas, FIX IT 
	 // voir test unitaire
	static getListOfLeekWhoPlayBefore(entity) {
		if(entity == self) return [];
		var list = [self]
		var self = self.turnOrder
		var limit = entity.turnOrder
		for(var e in _aliveAll) {
			if(self < limit){
				if(self < e.turnOrder && e.turnOrder < limit) push(list, e)
			} else {
				if(self < e.turnOrder) push(list, e)
				if(e.turnOrder < limit) push(list, e)
			}
		}
		return list;
	}
}